MODULE Receipt;
//определяем все сущности и атрибуты, необходимые для задания логики приходной накладной от поставщика.
//добавляем функции из других модулей
REQUIRE Stock, Item, LegalEntity;
//определим понятия шапка приходной накладной и строка приходной накладной.
CLASS Receipt 'Приходная накладная';
CLASS ReceiptDetail 'Строка приходной накладной';
//У каждой строки приходной накладной есть ссылка на шапку документа, и в итоге шапка документа и подмножество строк
//со ссылкой на этот документ в совокупности определяют приходную накладную с точке зрения пользователя. 
//Параметр NONULL обозначает, что ссылка должна быть задана. Параметр DELETE определяет, что при удалении основного объекта Receipt,
//все строки ReceiptDetail, ссылающиеся на него, будут также удалены. 
//По умолчанию, при удалении объекта все ссылки на него обнуляются. Таким образом, без параметра DELETE будет выдана ошибка, что ссылка не задана.
receipt 'Документ строки' = DATA Receipt (ReceiptDetail) NONULL DELETE;
//номер строки в приходной накладной
index 'Номер строки' (ReceiptDetail d) =
        PARTITION SUM 1 IF d IS ReceiptDetail
        ORDER d BY receipt(d);
        
//прописываем основные атрибуты шапки накладной
number 'Номер накладной' = DATA BPSTRING[10] (Receipt);
date 'Дата накладной' = DATA DATE (Receipt);

supplier 'Поставщик' = DATA LegalEntity (Receipt);
nameSupplier 'Наименование поставщика' (Receipt r) = name(supplier(r));

stock 'Склад' = DATA Stock (Receipt);
nameStock 'Наименование склада' (Receipt r) = name(stock(r));

//прописываем основные атрибуты строк накладной
item 'Товар' = DATA Item (ReceiptDetail);
nameItem 'Наименование товара' (ReceiptDetail d) = name(item(d));

quantity 'Количество' = DATA NUMERIC[16,4] (ReceiptDetail);
price 'Цена поставщика' = DATA NUMERIC[17,2] (ReceiptDetail);
sum 'Сумма поставщика' (ReceiptDetail d) = quantity(d) * price(d);

//форма добавления и редактирования приходной накладной. И указываем что будем использовать по умолчанию (Edit)
FORM receipt 'Приходная накладная'
    OBJECTS r = Receipt PANEL
    PROPERTIES(r) number, date, nameSupplier, nameStock

    OBJECTS d = ReceiptDetail
    PROPERTIES(d) index, nameItem, quantity, price, sum READONLY, NEW, DELETE GRID
    FILTERS receipt(d) = r

    EDIT Receipt OBJECT r
;
//Создаем форму приходных накладных. На форму выведем все свойства, определенные выше для шапок документов и их строк.
//Дополнительно выносим автоматически определенные кнопки добавления и редактирования приходной накладной с помощью 
//формы редактирования, созданной выше. Все свойства как шапки документов, так и их строк, кроме кнопок добавления и 
//редактирования приходной накладной, делаем недоступным для изменения непосредственно на форме (оператор READONLY).
FORM receipts 'Приходные накладные'
    OBJECTS r = Receipt
    PROPERTIES(r) READONLY number, date, nameSupplier, nameStock
    PROPERTIES(r) NEWSESSION NEW, EDIT, DELETE

    OBJECTS d = ReceiptDetail
    PROPERTIES(d) READONLY index, nameItem, quantity, price, sum
    FILTERS receipt(d) = r
;